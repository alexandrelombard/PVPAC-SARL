/** 
 * 
 */
package car

import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Destroy
import io.sarl.core.Initialize
import io.sarl.core.Schedules

import car.capacities.control.AccelerationControl
import car.capacities.control.AccelerationControlChange
import car.capacities.control.SteeringWheelControl
import car.capacities.control.SteeringWheelControlChange
import car.capacities.data.OdometryProvider
import car.capacities.data.PositionProvider
import car.events.PositionChanged
import car.events.SpeedChanged
import car.virtualcar.skills.ControlVirtualCar
import car.virtualcar.skills.PositionVirtualCar
import model.VirtualCar
import io.sarl.util.OpenEventSpace
import io.sarl.core.Behaviors
import io.sarl.core.Logging
import io.sarl.lang.core.Address
import utils.SpaceUtils
import simulation.Spaces
import io.sarl.util.OpenEventSpaceSpecification

/** 
 * @author alombard
 * 
 */
agent ControlVirtualCarAgent {

	uses Behaviors
	uses Logging
	uses DefaultContextInteractions, Schedules
	uses AccelerationControl, SteeringWheelControl
	uses PositionProvider, OdometryProvider
	
	private var internalVehicleSpace : OpenEventSpace
	private var environmentSpace : OpenEventSpace
	private var modelViewSpace : OpenEventSpace
	
	on Initialize {
		// Check parameters
		if (occurrence.parameters.length < 1) {
			throw new IllegalArgumentException("Internal vehicle space is not defined")
		}

		if (!(occurrence.parameters.get(0) instanceof OpenEventSpace)) {
			throw new IllegalArgumentException("First argument must be of type OpenEventSpace (internal vehicle space)")
		}
		
		// Initialize internal vehicle space
		this.internalVehicleSpace = occurrence.parameters.get(0) as OpenEventSpace
		this.internalVehicleSpace.register(asEventListener())
		
		// Initialize environment space
		this.environmentSpace = SpaceUtils.getOrCreateSpaceWithSpec(defaultContext, typeof(OpenEventSpaceSpecification), Spaces.ENVIRONMENT)
		this.environmentSpace.register(asEventListener())
		
		// Initialize model view space
		this.modelViewSpace = SpaceUtils.getOrCreateSpaceWithSpec(defaultContext, typeof(OpenEventSpaceSpecification), Spaces.MODEL_VIEW)
		this.modelViewSpace.register(asEventListener())
		
		// Initialize virtual car
		val virtualCar = new VirtualCar()
		
		// Add skill control virtual car
		setSkill(new ControlVirtualCar(virtualCar))
		
		// Add skill to position virtual car
		setSkill(new PositionVirtualCar(virtualCar))
		
		// Configure pro-active behavior (update car)
		every(50) [
			virtualCar.update(0.050)

			// Emit events in internal vehicle space
			this.internalVehicleSpace.emit(new PositionChanged(this.internalVehicleSpace.getAddress(this.ID), position, heading))
			this.internalVehicleSpace.emit(new SpeedChanged(this.internalVehicleSpace.getAddress(this.ID), speed))
			
			// Emit events in model-view space
			this.modelViewSpace.emit(new PositionChanged(this.modelViewSpace.getAddress(this.ID), position, heading))				
		]
	}
	
	on AccelerationControlChange {
		acceleration = occurrence.acceleration
	}
	
	on SteeringWheelControlChange {
		steeringWheelAngle = occurrence.steeringWheelAngle
	}
	
	on Destroy {
		//
	}
	
}
