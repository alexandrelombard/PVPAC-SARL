/** 
 * 
 */
package car

import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Destroy
import io.sarl.core.Initialize
import io.sarl.core.Schedules

import car.capacities.control.AccelerationControl
import car.capacities.control.AccelerationControlChange
import car.capacities.control.SteeringWheelControl
import car.capacities.control.SteeringWheelControlChange
import car.capacities.data.OdometryProvider
import car.capacities.data.PositionProvider
import car.events.PositionChanged
import car.events.SpeedChanged
import car.skills.virtualcar.ControlVirtualCar
import car.skills.virtualcar.PositionVirtualCar
import model.VirtualCar
import io.sarl.util.OpenEventSpace
import io.sarl.core.Behaviors
import io.sarl.core.Logging
import io.sarl.lang.core.Address
import utils.SpaceUtils
import simulation.Spaces
import io.sarl.util.OpenEventSpaceSpecification

/** 
 * @author alombard
 * 
 */
agent ControlVirtualCarAgent {

	uses Behaviors
	uses Logging
	uses DefaultContextInteractions, Schedules
	uses AccelerationControl, SteeringWheelControl
	uses PositionProvider, OdometryProvider
	
	private var internalVehicleSpace : OpenEventSpace
	private var environmentSpace : OpenEventSpace
	private var modelViewSpace : OpenEventSpace
	
	on Initialize {
		// Check parameters
		if (occurrence.parameters.length < 1) {
			throw new IllegalArgumentException("Internal vehicle space is not defined")
		}

		if (occurrence.parameters.length < 2) {
			throw new IllegalArgumentException("Environment space is not defined")
		}

		if (!(occurrence.parameters.get(0) instanceof OpenEventSpace)) {
			throw new IllegalArgumentException("First argument must be of type OpenEventSpace (internal vehicle space)")
		}

		if (!(occurrence.parameters.get(1) instanceof OpenEventSpace)) {
			throw new IllegalArgumentException("Second argument must be of type OpenEventSpace (environment space)")
		}
		
		// Register to appropriate spaces
		occurrence.parameters.forEach[o|if(o instanceof OpenEventSpace) o.register(asEventListener())]
		
		// Initialize internal vehicle space
		this.internalVehicleSpace = occurrence.parameters.get(0) as OpenEventSpace
		
		// Initialize environment space
		this.environmentSpace = occurrence.parameters.get(1) as OpenEventSpace
		
		// Initialize model view space
		this.modelViewSpace = SpaceUtils.getOrCreateSpaceWithSpec(defaultContext, typeof(OpenEventSpaceSpecification), Spaces.MODEL_VIEW)
		this.modelViewSpace.register(asEventListener())
		
		// Initialize virtual car
		val virtualCar = new VirtualCar()
		
		// Add skill control virtual car
		setSkill(new ControlVirtualCar(virtualCar))
		
		// Add skill to position virtual car
		setSkill(new PositionVirtualCar(virtualCar))
		
		// Configure pro-active behavior (update car)
		every(30) [
			virtualCar.update(0.030)

			// Emit events in internal vehicle space
			this.internalVehicleSpace.emit(new PositionChanged(this.internalVehicleSpace.getAddress(this.ID), position, heading))
			this.internalVehicleSpace.emit(new SpeedChanged(this.internalVehicleSpace.getAddress(this.ID), speed))
			
			// Emit events in model-view space
			if (this.modelViewSpace != null) {
				this.modelViewSpace.emit(new PositionChanged(this.modelViewSpace.getAddress(this.ID), position, heading))				
			} else {
				emit(new PositionChanged(this.modelViewSpace.getAddress(this.ID), position, heading))
			}
		]
	}
	
	on AccelerationControlChange {
		acceleration = occurrence.acceleration
	}
	
	on SteeringWheelControlChange {
		steeringWheelAngle = occurrence.steeringWheelAngle
	}
	
	on Destroy {
		//
	}
	
}
